"Pathogen needs to loaded before any other plugins and settings
call pathogen#runtime_append_all_bundles()

"{{{===GENERAL SETTINGS===
if !exists('g:macos')
    let g:macos=has('mac') || executable('pbcopy')
endif
set nocompatible
set nowritebackup
set fileencodings=ucs-bom,utf-8,gbk,gb18030,gb2312,default,latin1
if v:version < 703
	set number
else 
	set relativenumber
endif
set shiftwidth=4
set tabstop=4
set softtabstop=4
set expandtab
set backspace=2
set laststatus=2
set sessionoptions=blank,buffers,curdir,folds,help,localoptions,tabpages,winsize
" Broken down into easily includeable segments
set statusline=%f\    " Filename
set statusline+=%w%h%m%r " Options
set statusline+=%<%{fugitive#statusline()} "  Git Hotness
set statusline+=\ [%{&ff}/%Y]            " filetype
set statusline+=\ [%{getcwd()}]          " current dir
set statusline+=%=%-14.(%l,%c%V%)\ %p%%  " Right aligned file nav info
set ruler
set autoread
set incsearch
set ignorecase smartcase
set autoindent
set smartindent
set cinkeys=0{,0},!^F,o,O,e " default is: 0{,0},0),:,0#,!^F,o,O,e
set iskeyword=a-z,A-Z,48-57,_
set history=1000
set undolevels=1000
set conceallevel=2 concealcursor=i

"buffer setting
set switchbuf=useopen,usetab
"leave a buffer without saving
set hidden 
set wildchar=<Tab> wildmenu wildmode=full
:filetype plugin on 
:filetype indent on
:syntax on
"cscope
set cscopequickfix=s-,c-,d-,i-,t-,e-
if filereadable("cscope.out")
    cs add cscope.out
endif

"fix input method conflict
au InsertEnter * set noimd
au InsertLeave * set imd
" Insert and command-line mode Caps Lock.
" Lock search keymap to be the same as insert mode.
set imsearch=-1
" Load the keymap that acts like capslock.
set keymap=insert-only_capslock
set iminsert=0

au InsertLeave * set iminsert=0

:highlight Cursor guifg=NONE guibg=Green
:highlight lCursor guifg=NONE guibg=Cyan

if has('gui_running')
	set guioptions-=T           " remove the toolbar
	set guioptions-=L           " remove the scrollbar
	set guioptions-=r           " remove the scrollbar
	if has('gui_macvim')
		set guifont=Source\ Code\ Pro\ Light:h12
	endif
endif
set t_Co=256                 " Enable 256 colors to stop the CSApprox warning and make xterm vim shine
set background=dark
let g:solarized_termcolors=256
let g:solarized_contrast="high"
let g:solarized_visibility="high"
color solarized

"}}}
"===FILETYPE SETTING=== {{{
autocmd BufReadPost *
\ if line("'\"") > 0 && line ("'\"") <= line("$") |
\   exe "normal g'\"" |
\ endif

autocmd! BufWritePost *.sh silent !chmod +x %
autocmd! BufWritePost *.pl silent !chmod +x %

if &term=="xterm"
     set t_Co=8
     set t_Sb=[4%dm
     set t_Sf=[3%dm
endif

if v:lang =~ "utf8$" || v:lang =~ "UTF-8$"
   set fileencodings=utf-8,latin1
endif

"html settings
let g:html_indent_script1 = "inc" 
let g:html_indent_style1 = "inc" 
let g:html_indent_inctags = "html,body,head" 

" }}}
"===KEY MAPPING=== {{{
let mapleader = ","
nnoremap s ;
nnoremap S ,
imap <C-V> <C-R>"
imap <S-CR> <ESC>
vmap <S-CR> <ESC>
imap <C-B> <C-O>b
imap <C-F> <C-O>w
imap <C-H> <C-O>h
imap <C-J> <C-O>j
imap <C-K> <C-O>k
imap <C-L> <C-O>l
imap <C-A> <C-O>^
imap <C-E> <C-O>$
imap <C-S> <C-O>dw
imap <C-D> <C-O>x
inoremap # X<BS>#
inoremap <C-E> <C-^>
nmap <leader>nu :set number<CR>
nmap <leader>rn :set rnu<CR>
nmap <leader>xx :qa!<CR>
nmap j gj
nmap k gk
nmap <leader>te :tabnew<cr>
nmap <leader>tc :tabclose<cr>
nmap <leader>tn :tabn<cr>
nmap <leader>tp :tabp<cr>
nmap <leader>bn :bnext<cr>
nmap <leader>bp :bprev<cr>
nmap <leader>en :enew<cr>
nmap <leader>, :b#<cr>
nmap <leader>sb :sb#<cr>
nmap <leader>vs :vertical sb#<cr>
nmap <leader>wj <C-W>j
nmap <leader>wk <C-W>k
nmap <leader>wh <C-W>h
nmap <leader>wl <C-W>l
nmap <leader>w] <C-W>]
nmap <leader>wg] <C-W>g]
nmap <leader>J <C-W>J
nmap <leader>K <C-W>K
nmap <leader>H <C-W>H
nmap <leader>L <C-W>L
nmap <leader>wc :bd!<cr>
nmap <leader>we <C-W>=
nmap <leader>wz <C-W>z
nmap <leader>so :source %<CR>
nmap <leader>hl :let &hlsearch=1-&hlsearch<CR>
nmap <leader>ma :make<cr>
nmap <leader>cn :cnext<cr>
nmap <leader>cp :cprev<cr>
nmap <leader>cw :botright cwindow<cr>
nmap <leader>cl :ccl<cr>
nmap <leader>ln :lnext<cr>
nmap <leader>lp :lprev<cr>
nmap <leader>lw :botright lwindow<cr>
nmap <leader>lc :lclose<cr>
nmap <leader>cf :cs find s <C-R>=expand("<cword>")<CR><CR>
nmap <leader>@ "zyy:@z<CR>
vmap <leader>@ "zyy:@z<CR>
nmap <leader>ie /,\n\s*}<CR>
nnoremap <C-N> }
nnoremap <C-P> {
nnoremap <C-L> )
nnoremap ( [(
nnoremap ) ])
nnoremap { [{
nnoremap } ]}
nnoremap <C-J> 5j
nnoremap <C-K> 5k
vnoremap <C-N> }
vnoremap <C-P> {
vnoremap <C-L> )
vnoremap ( [(
vnoremap ) ])
vnoremap { [{
vnoremap } ]}
vnoremap <C-J> 5j
vnoremap <C-K> 5k
cmap <C-h> <LEFT>
cmap <C-k> <UP>
cmap <C-j> <DOWN>
cmap <C-l> <RIGHT>
cmap <C-B> <C-LEFT>
cmap <C-F> <C-RIGHT>
cmap <C-A> <HOME>
cmap <C-E> <END>
cmap <S-CR> <C-U><ESC>
cmap <C-V> <C-R><C-W>
cab vsb vertical sb
cab res resize
cab vres vertical resize
cab te tabe
cab cwd lcd %:p:h
cab cf :cs find 
cab ses mksession! session.vim
cab seo source session.vim
cab suw w !sudo tee %
cab erc e ~/.vim/.vimrc_common

"smart open
if g:macos
    nnoremap <leader>op :silent !open '<C-R>=expand('<cWORD>')<CR>'<CR>
    vnoremap <leader>op "zy:silent !open '<C-R>=getreg('z')<CR>'<CR>
endif

vnoremap <leader>yy "*y
noremap <leader>pp :set paste<CR>:put *<CR>:set nopaste<CR>
" }}}
"===PLUGIN SETTINGS=== {{{
"-Dash-
nmap <leader>D <Plug>DashSearch
vmap <leader>D "zy:Dash <C-R>=getreg('z')<CR><CR>
let g:dash_map = {
    \ 'objc'       : 'iphoneos',
    \ 'python'     : 'python2',
    \ }

"-TagBar-
nmap <leader>tt :TagbarToggle<CR>
nmap <leader>tj :TagbarOpen fj<CR>

"-NERDTree-
nmap <leader>nd :NERDTreeToggle<CR>
nmap <leader>nf :NERDTreeFind<CR>

"-NERDCommenter-
let g:NERDCreateDefaultMappings = 0
nmap <leader>cc <plug>NERDCommenterComment
vmap <leader>cc <plug>NERDCommenterComment
nmap <leader>cu <plug>NERDCommenterUncomment
vmap <leader>cu <plug>NERDCommenterUncomment
nmap <leader>cs <plug>NERDCommenterSexy
vmap <leader>cs <plug>NERDCommenterSexy
nmap <leader>c<space> <plug>NERDCommenterToggle
vmap <leader>c<space> <plug>NERDCommenterToggle

"-CtrlP-
let g:ctrlp_map = ';d'
nmap ;a :CtrlPMixed<CR>
nmap ;s :CtrlPBuffer<CR>
nmap ;f :CtrlPMRU<CR>
nmap ;r :CtrlP <C-R>=expand('%:p:h')<CR><CR>
nmap ;t :CtrlPTag<CR>
nmap ;g :CtrlPBufTag<CR>
nmap ;h :CtrlPClearCache<CR>:echo 'CtrlP file cache has been cleared!'<CR>
nmap ;H :CtrlPClearAllCaches<CR>:echo 'CtrlP cache has been cleared!'<CR> 
nmap ;c :CtrlPCmdPalette<CR>
let g:ctrlp_extensions = ['buffertag', 'tag', 'mixed']
let g:ctrlp_buftag_types = { 'objc' : {'bin' : 'objctags.py', 'args' : ''}}

"-Neocomplcache-
let g:neocomplcache_enable_at_startup = 1
let g:neocomplcache_max_list = 20
let g:neocomplcache_plugin_disable = {'include_complete' : 1, 'filename_complete' : 1}
let g:neocomplcache_min_syntax_length = 3
let g:neocomplcache_auto_completion_start_length = 3
let g:neosnippet#snippets_directory = expand("~/.vim/snippets")
if !exists('g:neocomplcache_omni_functions')
    let g:neocomplcache_omni_functions = {}
endif
let g:neocomplcache_omni_functions.python = 'python_complete#Complete'

" Make Neocomplache and clang_complete play together
if !exists('g:neocomplcache_force_omni_patterns')
    let g:neocomplcache_force_omni_patterns = {}
endif
let g:neocomplcache_force_overwrite_completefunc = 1
let g:neocomplcache_force_omni_patterns.objc = '_____'
if !exists('g:neocomplcache_omni_patterns')
    let g:neocomplcache_omni_patterns = {}
endif
let g:neocomplcache_omni_patterns.objc = '_____'
"let g:neocomplcache_force_omni_patterns.c ='[^.[:digit:] *\t]\%(\.\|->\)'
"let g:neocomplcache_force_omni_patterns.cpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
"let g:neocomplcache_force_omni_patterns.objc = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
"let g:neocomplcache_force_omni_patterns.objcpp = '[^.[:digit:] *\t]\%(\.\|->\)\|\h\w*::'
let g:clang_complete_auto = 0
let g:clang_debug = 1
let g:clang_auto_select = 0
let g:clang_use_library = 1
let g:clang_periodic_quickfix = 0
let g:clang_snippets = 1
let g:clang_close_preview = 1
let g:clang_library_path = '/usr/lib'

imap <expr><tab> neosnippet#expandable_or_jumpable() ? "\<Plug>(neosnippet_expand_or_jump)": "\<tab>"

"-Fugitive-
nmap <leader>gs :Gstatus<CR>
nmap <leader>gd :Gdiff<CR>
nmap <leader>gc :Gcommit -a<CR>
nmap <leader>gb :Gblame<CR>
nmap <leader>gl :Glog<CR>
nmap <leader>gp :Git push<CR>
nmap <leader>gg :Ggrep <C-R>=expand('<cword>')<CR><CR>
vmap <leader>gg "zy:Ggrep <C-R>=getreg('z')<CR><CR>

"-Tabular-
nmap <Leader>a= :Tabularize /=<CR>
vmap <Leader>a= <ESC>:'<,'>Tabularize /=<CR>
nmap <Leader>a: :Tabularize /:<CR>
vmap <Leader>a: <ESC>:'<,'>Tabularize /:<CR>
nmap <Leader>a<bar> :Tabularize /<bar><CR>
vmap <Leader>a<bar> <ESC>:'<,'>Tabularize /<bar><CR>

"-ConqueTerm
let g:ConqueTerm_SessionSupport = 1
let g:ConqueTerm_InsertOnEnter = 1
let g:ConqueTerm_CloseOnEnd = 1

"-Syntastic
let g:syntastic_auto_loc_list = 0
let g:syntastic_auto_jump = 1
let g:syntastic_python_checkers = ['pylint']
let g:syntastic_python_pylint_args = "-E --msg-template='{path}:{line}: [{msg_id}({symbol}), {obj}] {msg}' -r n"
let g:syntastic_mode_map = {
	\ 'mode' : 'passive',
	\ 'active_filetypes' : ['coffee', 'less']
\ }
nmap <leader>li :SyntasticCheck<CR>:Errors<CR>
let g:syntastic_objc_checker = 'clang'
let g:syntastic_objc_config_file = '.clang_complete'

"-TAssert
let g:TLOG='Decho'

"-Decho
let g:decho_winheight=15
let g:dechomode=3
function! DechoSystem(cmd)
	let result = system(a:cmd)
	for line in split(result, '\n')
		call Decho(line)
	endfor
endfunction

"Perl Settings
let perl_fold = 1
let perl_nofold_packages = 1

"Python Settings
let pydoc_highlight = 0

"Vim-Ref
let g:ref_jquery_path=$HOME . '/.vim/ref-doc/jquery'

"Quickrun
map <unique> <Leader>qr <Plug>(quickrun)

"IPython Shell
nmap <leader>ip :IPythonShellToggle<CR>
nmap <leader>ij :IPythonShellFocus<CR>
vmap <leader>is <esc>:IPythonShellSendText<CR>
let g:ipy_completefunc = ''

" }}}
"===COMMANDS & FUNCS=== {{{
command! -nargs=1 SearchCurrentFile :call s:SearchCurrentFile(<q-args>)
nmap <leader>* :SearchCurrentFile <C-R>=expand('<cword>')<CR><CR>
vmap <leader>* "zy:SearchCurrentFile <C-R>=getreg('z')<CR><CR>

function! s:SearchCurrentFile(pattern)
	let searchcmd = 'vimgrep '. a:pattern . ' ' . expand("%")
	exec searchcmd
endfunction

command! -nargs=1 SmartGrep :call s:SmartGrep(<q-args>)
command! -nargs=1 SmartReplace :call s:SmartReplace(<q-args>)
cab sgrep SmartGrep
cab srep SmartReplace
nmap <leader>gr :SmartGrep <C-R>=expand('<cword>')<CR><CR>
vmap <leader>gr "zy:SmartGrep <C-R>=getreg('z')<CR><CR>
nmap <leader>re :SmartReplace <C-R>=expand('<cword>')<CR><CR>
vmap <leader>re "zy:SmartReplace <C-R>=getreg('z')<CR><CR>
let g:smartGrep_rules = []

function! LookupDirs(curfile)
	let dir = "**/*." . expand("%:t:e")
	for rule in g:smartGrep_rules
		if a:curfile =~ rule[0]
			let dir = rule[1]
			break
		endif
	endfor
	return dir
endfunction

function! s:SmartGrep(pattern)
	let curfile = bufname('%')
	let grepcmd = 'silent grep ' . a:pattern . ' ' . LookupDirs(curfile)
	exec grepcmd
	exec 'botright cwindow'
	exec 'redraw!'
endfunction

function! s:SmartReplace(pattern, ...)
	let sub = a:0 > 1 ? a:1 : input("Replace '" . a:pattern . "' with: ")
	if sub == ''
		return
	endif
	exec 'wa'
	let curfile = bufname('%')
	let grepcmd = 'grep -nH ' . a:pattern . ' ' . LookupDirs(curfile)
	let matches = system(grepcmd)
	let match_list = split(matches, '\n')
	let match_files = []
	for i in range(len(match_list))
		let filename = substitute(match_list[i], ':.*$', '', '')
		if count(match_files, filename) == 0
			call add(match_files, filename)
		endif
	endfor
	let choice = input(matches . "Are you sure to replace all those occurences with '" . sub . "'([y]es/[n]o/[c]onfirm)?")
	if choice == 'y' || choice == 'Y' || choice == 'c' || choice == 'C'
		exec 'args ' . join(match_files, ' ')
		let confirm = (choice == 'c' || choice == 'C') ? 'c' : ''
		exec 'argdo %s/' . a:pattern . '/' . sub . '/g' . confirm
	endif
endfunction

"hex
if has('Python')
    nmap ,h :call HexValueInfo()<CR>
endif

function! HexValueInfo()
python <<EOF
    import vim, re, struct
    val = vim.eval('expand("<cword>")')
    val = eval(val)
    fval = struct.unpack('f', struct.pack("I", val))[0]
    #add comma to decimal value
    dval = str(val)
    dval = dval[::-1]
    dval = re.sub(r'(\d\d\d)(?=\d)',r'\1,',dval)
    dval = dval[::-1]
    print "val: 0x%x/%s/%g" % (val, dval, fval)
EOF
endfunction

"Paste mod
nmap <leader>pa :call TogglePasteMode()<CR>
function! TogglePasteMode()
	if &paste == 1
		let &paste = 0
		echo 'Paste Mode Off'
	else
		let &paste = 1
		echo 'Paste Mode On'
	endif
endfunction
" }}}

" vim: foldmethod=marker
